/*
 * Copyright (c) 2017   Mathijs Lagerberg, Pixplicity BV
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {

    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/.readme'
        exclude '.readme'
        return void
    }

    defaultConfig {

        minSdkVersion 14
        targetSdkVersion 25
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
        buildConfigField "String", "VERSION_STR", "\"${VERSION_NAME}-${VERSION_CODE} (${gitSha})\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            // TODO enable this
            minifyEnabled false
            consumerProguardFiles 'consumer-proguard-rules.pro'
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds true
        lintConfig file("${project.rootDir}/config/lint.xml")
        xmlOutput file("${project.buildDir}/reports/lint/lint-report.xml")
        htmlOutput file("${project.buildDir}/reports/lint/lint-report.html")
    }
}

check {
    abortOnError false

    checkstyle {
        abortOnError false
        config "${project.rootDir}/config/checkstyle.xml"
        report new File(project.buildDir, 'reports/checkstyle')
        reportXML new File(project.buildDir, 'reports/checkstyle/checkstyle.xml')
        reportHTML new File(project.buildDir, 'reports/checkstyle/checkstyle.html')
    }

    pmd {
        skip false
        abortOnError false
        config "${project.rootDir}/config/pmd.xml"
        report new File(project.buildDir, 'reports/pmd')
        reportXML new File(project.buildDir, 'reports/pmd/pmd.xml')
        reportHTML new File(project.buildDir, 'reports/pmd/pmd.html')
    }

    findbugs {
        skip false
        abortOnError false
        config "${project.rootDir}/config/findbugs.xml"
        report new File(project.buildDir, 'reports/findbugs')
        reportXML new File(project.buildDir, 'reports/findbugs/findbugs.xml')
        reportHTML new File(project.buildDir, 'reports/findbugs/findbugs.html')
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            group "com.pixplicity.libraries"
            artifactId LIBRARY_NAME
            version VERSION_NAME
        }
    }
}

// Thanks to https://guides.codepath.com/android/Building-your-own-Android-library
bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintray.apikey') ? project.property('bintray.apikey') : System.getenv('BINTRAY_API_KEY')
    // jFrog plugin must be declared for this line to work
    configurations = ['archives']
    // Package info for BinTray
    pkg {
        repo = 'android'
        name = LIBRARY_NAME
        userOrg = 'pixplicity'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Pixplicity/gene-rate'
        version {
            name = '1.0'
            desc = "Android Gene-rate Library 1.0"
            released = new Date()
            vcsTag = '1.0'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.0'
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:design:25.1.0'
}
